(* Parser for block content *)
(* TODO should make more use of instaparse compositionality *)

block = heading | blockquote | (syntax-in-block / text)*

<syntax-in-block> = (todo | done | image | alias | block-embed | hashtag | page-link | block-ref | block-property | code-line | code-block | youtube | bold | italic | bare-url | page-alias | hr | latex ) (* add query later*)

(* page links do not want their innards parsed *)
page-link = #"\[\[.*?\]\]" 
alias = #"\[([^\[\]]+?)\]\(.+?\)"  (* Note this hairy but correct char class that means everything but square brackets *)
image = #"!\[(.*?)\]\(.*?\)"   (* image = "!" alias doesn't quite work *)
hashtag = #"\#[\w-:]+" | #"#\[\[.*?\]\]"
block-ref = #"\(\(([\w\s\d-]+)\)\)"
(* metadata-tag = #"^.+?::" *) (* Roam *)
block-property = #"(?m)^.+?::.+?$"
code-line = #"\`.*?\`"
code-block = #"(?is)^```.*```"
(* query = #"\{\{query: .*?:.*?\}\}" | #"\{\{[[query]]: .*?:.*?\}\}" *)
youtube = #"\{\{youtube: .*?\}\}" | #"\{\{\[\[youtube\]\]: .*?:.*?\}\}" | #"\{\{\[\[video\]\]: .*?:.*?\}\}"
page-alias = #"\{\{alias\:\[\[.+\]\].*\}\}"
bold = <"**">, (!bold syntax-in-block / textier)* , <"**">
italic = <"__">, (!italic syntax-in-block / textier)* , <"__">
(* Don't use these so turning off for performance
 strikethrough = "~~", (syntax-in-block / text)* , "~~"
 highlight = "^^", (syntax-in-block / text)* , "^^"  *)
todo = "{{[[TODO]]}}"
done = "{{[[DONE]]}}"
block-embed = #"\{\{embed: .*?\}\}" | #"\{\{\[\[embed\]\]: .*?\}\}"
bare-url = #"(https?:\/\/(?:www\.|(?!www))[a-zA-Z0-9][a-zA-Z0-9-]+[a-zA-Z0-9]\.[^\s]{2,}|www\.[a-zA-Z0-9][a-zA-Z0-9-]+[a-zA-Z0-9]\.[^\s]{2,}|https?:\/\/(?:www\.|(?!www))[a-zA-Z0-9]+\.[^\s]{2,}|www\.[a-zA-Z0-9]+\.[^\s]{2,})"
blockquote = <"> "> block
hr = "---"
latex= #"\$\$(.*?)\$\$"

(* should match all punc that is not otherwise used for syntax, not sure I have everything  *)
text = #'[A-Za-z0-9\.\,\!\"\'\?]+|[\s\W_]+?'
textier = #'[A-Za-z0-9\.\,\!\"\'\?]+|[\s\W]+?' 

heading = #"#{1,3}" block

