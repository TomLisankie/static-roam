(* Parser for block content *)
block = blockquote | (syntax-in-block / any-chars)*

<syntax-in-block> = (todo | done | image | alias | block-embed | hashtag | page-link | block-ref | metadata-tag | code-line | code-block | youtube | bold | italic | strikethrough | highlight | bare-url ) (* add query later*)
image = #"!\[(.*?)\]\(.*?\)"
page-link = #"\[\[.*?\]\]"
alias = #"\[([^\[\]]*?)\]\(.*?\)"  (* Note this hairy but correct char class that means everything but square brackets *)
hashtag = #"\#[\w-]*" | #"#\[\[.*?\]\]"
block-ref = #"\(\(([\w\s\d]+)\)\)"
metadata-tag = #"^.+?::"
code-line = #"\`.*?\`"
code-block = #"(?is)^```.*```"
(* query = #"\{\{query: .*?:.*?\}\}" | #"\{\{[[query]]: .*?:.*?\}\}" *)
youtube = #"\{\{youtube: .*?\}\}" | #"\{\{\[\[youtube\]\]: .*?:.*?\}\}" | #"\{\{\[\[video\]\]: .*?:.*?\}\}"
bold = #"\*\*.*?\*\*"
italic = #"\_\_.*?\_\_"
strikethrough = #"\~\~.*?\~\~"
highlight = #"\^\^.*?\^\^"
todo = #"\{\{\[\[TODO\]\]\}\}"
done = #"\{\{\[\[DONE\]\]\}\}"
block-embed = #"\{\{embed: .*?\}\}" | #"\{\{\[\[embed\]\]: .*?\}\}"
bare-url = #"(https?:\/\/(?:www\.|(?!www))[a-zA-Z0-9][a-zA-Z0-9-]+[a-zA-Z0-9]\.[^\s]{2,}|www\.[a-zA-Z0-9][a-zA-Z0-9-]+[a-zA-Z0-9]\.[^\s]{2,}|https?:\/\/(?:www\.|(?!www))[a-zA-Z0-9]+\.[^\s]{2,}|www\.[a-zA-Z0-9]+\.[^\s]{2,})"
blockquote = <"> "> block

any-chars = #'[A-Za-z0-9]+|[\s\W_]+?'
